#!/usr/bin/env fish
argparse 'p/parent=' -- $argv
or return

set origin origin

if set -q _flag_parent
    set parent "$_flag_parent"
    echo "Branching off $parent"
else
    echo "Branching off "(git branch --show-current)
end

set user justus

set date (date -I)

if test -n "$argv[1]"
    set branchname "$argv[1]"
else if read -P "Enter new branch name: " branchname
    echo "Using $branchname as new branch name"
else
    echo "Must enter branch name"
    exit 1
end

if ! git fetch "$origin"
    echo "Could not fetch from $origin"
    exit 1
end

if set -q parent
    git checkout -b "$user/$date-$branchname" "$origin/$parent"
else
    git checkout -b "$user/$date-$branchname"
end; or return
