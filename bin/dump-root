#!/usr/bin/env fish
# SPDX-FileCopyrightText: 2014-2025 Justus Perlwitz
#
# SPDX-License-Identifier: GPL-3.0-or-later
#
# Expects $recipient to be set to an email address in our gpg keychain
# TODO Sign our dumps
# TODO Exit trap lvremove

function cleanup --on-process-exit $fish_pid --on-signal SIGINT
    echo "Removing snapshot at $snap"
    sudo lvremove --yes $snap
end

set bs 4M
set vg_name helium--vg
set root_name "$vg_name-root"
set fs "/dev/mapper/$root_name"
set snap_name "rootsnap$(openssl rand -hex 8)"
set snap "/dev/mapper/$vg_name-$snap_name"
set out_dir /srv/dumping-grounds/dump
set dest "$out_dir/$root_name-$(date -R).gz.gpg"

echo "Creating snapshot of $root_name at $snap"
if sudo lvcreate --size 8G --snapshot --name $snap_name $fs
    echo "Snapshot created"
    lsblk $snap
    df -h $snap
    set size (lsblk $snap | awk 'NR==2{print $4}')
    echo "Approximate size of snapshot is $size"
else
    echo "Unable to create snapshot"
    exit 1
end

echo "Assuming recipient is $recipient"
echo "Dumping $snap to $dest"

if sudo dd if=$snap bs=$bs |
        pv --cursor --name Dump --size $size |
        gzip --stdout - |
        gpg --encrypt --recipient $recipient |
        pv --cursor --name "Compressed and gpg'ed" |
        sudo tee $dest | xxd
    echo "Pipestatus $pipestatus"
    echo "Dumping complete"
else
    echo "Dumping did not work, removing archive at $dest"
    sudo rm $dest
    exit 1
end
