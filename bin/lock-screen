#!/usr/bin/env fish
# SPDX-FileCopyrightText: 2014-2025 Justus Perlwitz
#
# SPDX-License-Identifier: GPL-3.0-or-later
#
# Lock screen on sway

if [ -n "$argv[1]" ]
    set message $argv[1]
else
    set message "Hello, World!"
end

if set font (fc-match sans file | cut -d= -f2)
    set font_flag -font $font
else
    echo "Couldn't find font"
    set font_flag
end

if set output_file (mktemp --suffix=.png) && \
    magick \
        $font_flag \
        -size 1024x768 xc:white \
        -gravity center \
        -pointsize 24 \
        -fill black \
        -annotate +0+0 \
        $message \
        $output_file
    set image_flag --image $output_file
else
    echo "Could not create idle message"
    set image_flag --color ff6e67
end

exec swaylock --show-failed-attempts $image_flag
